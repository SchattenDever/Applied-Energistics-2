/*
 * This file is part of Applied Energistics 2.
 * Copyright (c) 2013 - 2015, AlgorithmX2, All rights reserved.
 *
 * Applied Energistics 2 is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Applied Energistics 2 is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public License
 * along with Applied Energistics 2.  If not, see <http://www.gnu.org/licenses/lgpl>.
 */

plugins {
    id 'idea'
    id 'net.minecraftforge.gradle' version '[6.0.16,6.2)'
}

version = aeversion + "-" + aechannel + "-" + aebuild
group = aegroup

base {
    archivesName = aebasename
}

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

minecraft {
    mappings channel: mapping_channel, version: mapping_version

    copyIdeResources = true

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        configureEach {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
        }

        client {
            property 'forge.enabledGameTestNamespaces', aebasename
        }

        server {
            property 'forge.enabledGameTestNamespaces', aebasename
            args '--nogui'
        }

        data {
            workingDirectory project.file('run-data')

            args '--mod', aebasename, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}


sourceSets {
    main {
        resources {
            srcDir 'src/generated/resources'
        }
    }
}

apply from: 'gradle/scripts/dependencies.gradle'
apply from: 'gradle/scripts/artifacts.gradle'

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
}

artifacts {
    jar
}

tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [
            version: project.version,
            mcversion: minecraft_version,
            forge_version: forge_version
    ]
    inputs.properties replaceProperties
    filesMatching(['mcmod.info']) {
        expand replaceProperties + [project: project]
    }
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                'FMLAT': 'accesstransformer.cfg'
        ])
    }

    var replaceProperties = [
            "@version@": project.version,
            "@aeversion@": aeversion,
            "@aechannel@": aechannel,
            "@aebuild@": aebuild
    ]
    inputs.properties replaceProperties
    filesMatching(['appeng/core/AEConfig.java', 'appeng/api/package-info.java']) {
        expand replaceProperties
    }

    // This is the preferred method to reobfuscate your jar file
    finalizedBy 'reobfJar'
}
